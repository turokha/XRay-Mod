plugins {
    id 'java'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = 'pro.mikey'

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'

    version = mod_version

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    idea {
        module {
            downloadSources = true
            downloadJavadoc = true
        }
    }

//    jar {
//        from(rootProject.file("LICENSE").toString()) {
//            rename { "${it}_${project.archivesBaseName}" }
//        }
//    }
}

subprojects {
    base {
        archivesName = "advanced-xray-$project.name"
    }

    repositories {
        maven {
            url "https://libraries.minecraft.net"
            content {
                includeModule("org.lwjgl", "lwjgl-freetype")
            }
        }
    }

    if (project.name != "common") {
        tasks.withType(JavaCompile).configureEach {
            if (!it.name.toLowerCase().contains("test")) {
                source(project(":common").sourceSets.main.allSource)
            }
        }

        tasks.withType(Javadoc).configureEach {
            source(project(":common").sourceSets.main.allJava)
        }

        processResources {
            from project(":common").sourceSets.main.resources
        }
    }

    def replaceProperties = [
            minecraft_version: minecraft_version,
            minecraft_version_range: minecraft_version_range.replace("[%base]", minecraft_version),
            forge_version_range: forge_version_range,
            version: version,
    ]

    processResources {
        inputs.properties replaceProperties
    }
}
